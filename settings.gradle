pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google.*")
                includeGroupByRegex("androidx.*")
            }
        }
        mavenCentral()
        gradlePluginPortal {
            content {
                includeModule("com.gradle", "develocity-gradle-plugin")
                includeModule(
                    "com.gradle.develocity",
                    "com.gradle.develocity.gradle.plugin"
                )
                includeModule("io.github.gradle-nexus", "publish-plugin")
                includeModule(
                    "io.github.gradle-nexus.publish-plugin",
                    "io.github.gradle-nexus.publish-plugin.gradle.plugin"
                )
                includeModule("org.gradle", "github-dependency-graph-gradle-plugin")
                includeModule(
                    "org.gradle.github-dependency-graph-gradle-plugin",
                    "org.gradle.github-dependency-graph-gradle-plugin.gradle.plugin"
                )
                includeModule("org.spdx", "spdx-gradle-plugin")
                includeModule("org.spdx.sbom", "org.spdx.sbom.gradle.plugin")
            }
        }
    }
}

plugins {
    id "com.gradle.develocity" version "3.18.1"
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
}

enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")

develocity {
    buildScan {
        termsOfUseUrl = "https://gradle.com/help/legal-terms-of-use"
        termsOfUseAgree = System.getenv("CI") != null ? "yes" : "no"
    }
}

rootProject.name = "WebAuthnKt"
include(
    ":webauthn2-json-moshi",
    ":webauthn2-json-moshi-annotation",
    ":webauthn2-json-serialization",
    ":webauthn2-json-serialization-annotation"
)
